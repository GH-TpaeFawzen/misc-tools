#! /bin/sh
 
######################################################################
#
# URLENCODE - URL decoder on the basis of RFC 3986
#
# USAGE: urldecode <file> ...
#
# Written by Rich Mikan (richmikan[at]richlab.org) at 2014/11/22
#
# This is a public-domain software. It measns that all of the people
# can use this with no restrictions at all. By the way, I am fed up
# the side effects which are broght about by the major licenses.
#
######################################################################
 
# --- initalization --------------------------------------------------
PATH=/usr/bin:/bin
#
print_usage_and_exit () {
  cat <<-__USAGE 1>&2
	Usage   : ${0##*/} <file> ...
	Version : Sat Nov 22 13:22:54 JST 2014
__USAGE
  exit 1
}

# --- print help message if required ---------------------------------
case "${1:-}" in
  --help|--version|-h) print_usage_and_exit;;
esac

# --- main -----------------------------------------------------------
case $# in
  0) cat -                    ;;
  *) for file in "$@"; do      #
       case "$file" in      #  #
         -)  :             ;;  #
         /*) :             ;;  #
         *)  file="./$file";;  #
       esac                 #  #
       cat "$file"             #
     done                     ;;
esac |
(cat -; printf '\n') |
exec env - awk '
BEGIN {
  # --- prepare
  LF  = sprintf("\n");
  OFS = "";
  ORS = "";
  # --- prepare decoding
  for (i=0; i<256; i++) {
    l  = sprintf("%c",i);
    k1 = sprintf("%02x",i);
    k2 = substr(k1,1,1) toupper(substr(k1,2,1));
    k3 = toupper(substr(k1,1,1)) substr(k1,2,1);
    k4 = toupper(k1);
    p2c[k1]=l;p2c[k2]=l;p2c[k3]=l;p2c[k4]=l;
  }
  # --- decode
  while (getline line) {
    gsub(/\+/, " ", line);
    while (length(line)) {
      if (match(line,/%[0-9A-Fa-f][0-9A-Fa-f]/)) {
        print substr(line,1,RSTART-1), p2c[substr(line,RSTART+1,2)];
        line = substr(line,RSTART+RLENGTH);
      } else {
        print line;
        break;
      }
    }
    print LF;
  }
}' |
awk '
BEGIN{
  ORS="";
  OFS="";
  getline line;
  print line;
  dlm=sprintf("\n");
  while (getline line) {
    print dlm,line;
  }
}
'
